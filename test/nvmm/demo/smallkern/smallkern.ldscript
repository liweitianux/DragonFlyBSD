__PAGE_SIZE = 0x1000 ;

ENTRY(start)
SECTIONS
{
	.text : AT (ADDR(.text) & 0x0fffffff)
	{
		*(.text)
		*(.text.*)
		*(.stub)
	} =0xCC
	_etext = . ;
	PROVIDE (etext = .) ;

	. = ALIGN(__PAGE_SIZE);

	__rodata_start = . ;
	.rodata :
	{
		*(.rodata)
		*(.rodata.*)
	}

	. = ALIGN(__PAGE_SIZE);

	__data_start = . ;
	.data :
	{
		*(.data)
	}

	__bss_start = . ;
	.bss :
	{
		*(.bss)
		*(.bss.*)
		*(COMMON)
	}

	. = ALIGN(__PAGE_SIZE);

	/* End of the kernel image */
	__smallkern_end = . ;

	_end = . ;
	PROVIDE (end = .) ;
}

